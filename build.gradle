plugins {
    id 'maven-publish'
    id 'application'
}

ext {
    // dependencies version
    ioxIliVersion = '1.20.15'
    ioxWkfVersion = '1.2.23' // Temporary. Until shp-trim branch is merged into master / original repo.
    av2geobauVersion = '1.0.2-SNAPSHOT'
    sqliteJdbcVersion = '3.8.11.2'

    // dependencies
    ioxIliDependency = [ group: 'ch.interlis', name: 'iox-ili', version: ioxIliVersion ]
    ioxWkfDependency = [ group: 'ch.interlis', name: 'iox-wkf', version: ioxWkfVersion ]
    av2geobauDependency = [ group: 'ch.interlis', name: 'av2geobau', version: av2geobauVersion ]
    sqliteJdbcDependency = [ group: 'org.xerial', name: 'sqlite-jdbc', version: sqliteJdbcVersion ]
}

repositories {
    maven { url "https://dl.bintray.com/sogis/iox-wkf/" } // TODO: REMOVE!!
    maven { url "http://jars.interlis.ch" }
    maven { url "https://repo.osgeo.org/repository/release/" }
    jcenter()
}

dependencies {
    implementation ioxWkfDependency
    implementation ioxIliDependency
    implementation av2geobauDependency
    implementation sqliteJdbcDependency
}

application {
    // Define the main class for the application.
    mainClassName = 'ch.so.agi.Gpkg2Dxf'
}

// all source files are encoded as UTF-8
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}


jar {
    manifest {
        attributes(
                'Main-Class': 'ch.so.agi.Gpkg2Dxf',
                "Class-Path": configurations.runtimeClasspath.collect { 'libs/'+it.getName() }.join(' ')
        )
    }
}

task bindist(type: Zip){
	baseName = project.name
	destinationDir = file('dist')
	from jar
	into('libs'){
		from configurations.runtimeClasspath
	}
}

artifacts {
	archives(bindist.archivePath) {
        type 'zip'
        classifier 'bindist'
        builtBy bindist
    }
    archives(jar.archivePath) {
        builtBy jar
    }
}
